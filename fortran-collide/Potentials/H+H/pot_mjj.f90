!
! GENERATE POTENTIAL
!
!
      SUBROUTINE CALPOT(R,VPOT,ICASE)                               

! CALCULATES TWICE REDUCED MASS TIMES POTENTIAL

      IMPLICIT NONE

      DOUBLE PRECISION :: VSINGL, VTRIPL
      DOUBLE PRECISION :: R, VPOT
      DOUBLE PRECISION, PARAMETER :: RMASS = 1.0D+00

      INTEGER :: IREAD, ICASE
      INTEGER, PARAMETER :: ITRIP = 1
      INTEGER, PARAMETER :: ISING = 0

      DATA IREAD /0/
      SAVE IREAD

! CALCULATE ONCE AT R(1) TO ENSURE DATA IS READ

      IF (IREAD .EQ. 0) THEN
         ! CALCULATE ONCE AT R TO ENSURE DATA IS READ
         CALL POTENT(R,VSINGL,VTRIPL,RMASS,IREAD)
         IREAD = 1
      END IF

! CALCULATE AT ALL DATA POINTS
      CALL POTENT(R,VSINGL,VTRIPL,RMASS,IREAD)
      IF(ICASE.EQ.ISING) VPOT=VSINGL
      IF(ICASE.EQ.ITRIP) VPOT=VTRIPL

      RETURN                                                            
      END                                                               
!
!=============================================================
!
      SUBROUTINE POTENT(R,VSINGL,VTRIPL,REDMAS,IREAD)
!
! HYDROGEN-HYDROGEN SINGLET X-STATE AND TRIPLET B-STATE
!
! CALCULATES SINGLET AND TRIPLET POTENTIALS, VSINGL AND VTRIP,
! IN HARTREE AT INTERATOMIC SEPARATION R BOHR
!
! ONLY IF IREAD IS ZERO
!   READS TABLE OF SINGLET BORN OPPENHEIMER POTENTIALS FROM FORT.1
!   READS TABLE OF SINGLET ADIABATIC CORRECTIONS FROM FORT.2
!   READS TABLE OF SINGLET RELATIVISTIC CORRECTIONS FROM FORT.3
!   READS TABLE OF SINGLET RADIATIVE CORRECTIONS FROM FORT.4
!
!   READS TABLE OF TRIPLET BORN OPPENHEIMER POTENTIALS FROM FORT.7
!   READS TABLE OF SINGLET ADIABATIC CORRECTIONS FROM FORT.2
!
!   SETS UP TABLE OF SECOND DERIVATIVES FOR FITTING
!
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      DIMENSION RSBO(1000),VSBO(1000),DSBO(1000)
      DIMENSION RSAD(1000),VSAD(1000),DSAD(1000)
      DIMENSION RSREL(1000),VSREL(1000),DSREL(1000)
      DIMENSION RSRAD(1000),VSRAD(1000),DSRAD(1000)
      DIMENSION RTBO(1000),VTBO(1000),DTBO(1000)
      DIMENSION RTAD(1000),VTAD(1000),DTAD(1000)

      COMMON/REGION1/NSBO,NSAD,NSREL,NSRAD,NTBO,NTAD
      COMMON/REGION2/RSBO,VSBO,DSBO,RSAD,VSAD,DSAD,RSREL,VSREL,DSREL, &
      &    RSRAD,VSRAD,DSRAD,RTBO,VTBO,DTBO,RTAD,VTAD,DTAD
!
! IPR=1 TO PRINT
! IPR=0 NOT TO PRINT
!
      IPR=0
!
! IALL=0 FOR BORN OPPENHEIMER ONLY
! IALL=1 FOR ADIABATIC, RELATIVISTIC AND RADIATIVE SINGLET CORRECTIONS 
!
      IALL=1
!
! ATOMIC UNITS (HARTREE) TO INVERSE CM CONVERSION
!
      AUTOCM=219474.631D0
!
! ISBOPP=1 FOR SINGLET BORN OPPENHEIMER
! IASDIA=2 FOR SINGLET ADIABATIC
! ISREL=3 FOR SINGLET RELATIVISTIC
! ISRAD=4 FOR SINGLET RADIATIVE
! ITBOPP=7 FOR TRIPLET BORN OPPENHEIMER
! ITADIA=8 FOR TRIPLET ADIABATIC

      ISBOPP=1
      ISADIA=2
      ISREL=3
      ISRAD=4
      ITBOPP=7
      ITADIA=8
!
! READ SINGLET BORN OPPENHEIMER AND CALCULATE SECOND DERIVATIVES
!
      IF(IREAD.EQ.0) CALL RDATA(ISBOPP,RSBO,VSBO,NSBO,VSBOP1,VSBOPN)
      IF(IREAD.EQ.0) CALL DERIVE(RSBO,VSBO,DSBO,NSBO,VSBOP1,VSBOPN)

! READ SINGLET ADIABATIC AND CALCULATE SECOND DERIVATIVES

      IF(IREAD.EQ.0) CALL RDATA(ISADIA,RSAD,VSAD,NSAD,VSADP1,VSADPN)
      IF(IREAD.EQ.0) CALL DERIVE(RSAD,VSAD,DSAD,NSAD,VSADP1,VSADPN)

! READ SINGLET RELATIVISTIC AND CALCULATE SECOND DERIVATIVES

      IF(IREAD.EQ.0) CALL RDATA(ISREL,RSREL,VSREL,NSREL,VSREP1,VSREPN)
      IF(IREAD.EQ.0) CALL DERIVE(RSREL,VSREL,DSREL,NSREL,VSREP1,VSREPN)

! READ SINGLET RADIATIVE AND CALCULATE SECOND DERIVATIVES

      IF(IREAD.EQ.0) CALL RDATA(ISRAD,RSRAD,VSRAD,NSRAD,VSRAP1,VSRAPN)
      IF(IREAD.EQ.0) CALL DERIVE(RSRAD,VSRAD,DSRAD,NSRAD,VSRAP1,VSRAPN)

! READ TRIPLET BORN OPPENHEIMER AND CALCULATE SECOND DERIVATIVES

      IF(IREAD.EQ.0) CALL RDATA(ITBOPP,RTBO,VTBO,NTBO,VTBOP1,VTBOPN)
      IF(IREAD.EQ.0) CALL DERIVE(RTBO,VTBO,DTBO,NTBO,VTBOP1,VTBOPN)

! READ TRIPLET ADIABATIC AND CALCULATE SECOND DERIVATIVES

      IF(IREAD.EQ.0) CALL RDATA(ITADIA,RTAD,VTAD,NTAD,VTADP1,VTADPN)
      IF(IREAD.EQ.0) CALL DERIVE(RTAD,VTAD,DTAD,NTAD,VTADP1,VTADPN)


      DO 20 ICASE=1,4
      CALL SINPOT(RSBO,VSBO,DSBO,NSBO,       &
     &         RSAD,VSAD,DSAD,NSAD,REDMAS,   &
     &         RSREL,VSREL,DSREL,NSREL,			 &
     &         RSRAD,VSRAD,DSRAD,NSRAD,ICASE,R,VSING)

! SINGLET BORN OPPENHEIMER

      IF(ICASE.EQ.ISBOPP) VSBORN=VSING
      IF(ICASE.EQ.ISBOPP.AND.IPR.EQ.1) WRITE(6,11) R,VSBORN
   11 FORMAT(1X,'SINGLET BORN OPPENHEIMER (AU) ',F8.3,D13.6)

! SINGLET ADIABATIC

      IF(ICASE.EQ.ISADIA) VSADIA=VSING
      IF(ICASE.EQ.ISADIA.AND.IPR.EQ.1) WRITE(6,12) R,VSADIA*AUTOCM
   12 FORMAT(1X,'SINGLET ADIABATIC (INV CM)    ',F8.3,D13.6)

! SINGLET RELATIVISTIC

      IF(ICASE.EQ.ISREL) VSRELA=VSING
      IF(ICASE.EQ.ISREL.AND.IPR.EQ.1) WRITE(6,13) R,VSRELA*AUTOCM
   13 FORMAT(1X,'SINGLET RELATIVISTIC (INV CM) ',F8.3,D13.6)

! SINGLET RADIATIVE

      IF(ICASE.EQ.ISRAD) VSRADI=VSING
      IF(ICASE.EQ.ISRAD.AND.IPR.EQ.1) WRITE(6,14) R,VSRADI*AUTOCM
   14 FORMAT(1X,'SINGLET RADIATIVE (INV CM)    ',F8.3,D13.6)

   20 CONTINUE

      IF(IPR.EQ.1) WRITE(6,22)
   22 FORMAT(/)

      DO 21 ICASE=7,8
      CALL TRIPOT(RTBO,VTBO,DTBO,NTBO,  &
     &         RTAD,VTAD,DTAD,NTAD,REDMAS,ICASE,R,VTRIP)

! TRIPLET BORN OPPENHEIMER

      IF(ICASE.EQ.ITBOPP) VTBORN=VTRIP
      IF(ICASE.EQ.ITBOPP.AND.IPR.EQ.1) WRITE(6,15) R,VTBORN
   15 FORMAT(1X,'TRIPLET BORN OPPENHEIMER (AU) ',F8.3,D13.6)

! TRIPLET ADIABATIC

      IF(ICASE.EQ.ITADIA) VTADIA=VTRIP
      IF(ICASE.EQ.ITADIA.AND.IPR.EQ.1) WRITE(6,16) R,VTADIA*AUTOCM
   16 FORMAT(1X,'TRIPLET ADIABATIC (INV CM)    ',F8.3,D13.6)

   21 CONTINUE

      IF(IPR.EQ.1) WRITE(6,22)

! SINGLET TOTAL

      VSTOT=VSBORN+(VSADIA+VSRELA+VSRADI)*IALL
      VSINGL=VSTOT
      IF(IPR.EQ.1) WRITE(6,17) R,VSINGL
   17 FORMAT(1X,'SINGLET TOTAL (AU)            ',F8.3,D13.6)

! TRIPLET TOTAL

      VTTOT=VTBORN+VTADIA
      VTRIPL=VTTOT
      IF(IPR.EQ.1) WRITE(6,18) R,VTRIPL
   18 FORMAT(1X,'TRIPLET TOTAL (AU)            ',F8.3,D13.6)

      IF(IPR.EQ.1) WRITE(6,19)
   19 FORMAT(1X,70('='))
      RETURN
      END
!
!=============================================================
!
      SUBROUTINE RDATA(ICHAN,XVEC,YVEC,NVEC,YP1,YPN)

!
! READS NVEC VALUES IN TABLE [XVEC,YVEC] AND 
! DERIVATIVES AT ENDS, YP1 AND YPN, FOR CUBIC SPLINE FIT
!
! ICHAN IS CHANNEL NUMBER FOR INPUT
!
! ICHAN=1  FOR SINGLET BORN-OPPENHEIMER
! ICHAN=2  FOR SINGLET ADIABATIC
! ICHAN=3  FOR SINGLET RELATIVISTIC
! ICHAN=4  FOR SINGLET RADIATIVE
! ICHAN=7  FOR TRIPLET BORN-OPPENHEIMER
! ICHAN=8  FOR TRIPLET ADIABATIC
!
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      DIMENSION XVEC(1000),YVEC(1000)
			CHARACTER(LEN=24)		:: fname
			WRITE(fname,"(A,I1)") "../Potentials/H+H/fort.", ICHAN
			OPEN(UNIT=66,FILE=fname,STATUS="OLD",ACTION="READ")
! READ CHANNEL NUMBER FOR CHECK
      READ(66,*) NCHAN

      IF(ICHAN.EQ.NCHAN) GO TO 1
      IF(IPR.EQ.1) WRITE(6,10)
   10 FORMAT(1X,'CHANNEL NUMBER WRONG IN INPUT')
      STOP

    1 CONTINUE

! READ NUMBER OF POINTS FOR INPUT

      READ(66,*) NVEC

! READ VALUE AT INFINITY WHICH IS TO BE SUBTRACTED FROM ALL VALUES
! TO GIVE POTENTIALS REFERRED TO VALUE AT INFINITY

      READ(66,*) VATINF

! READ FACTOR TO CONVERT TO ATOMIC UNITS

      READ(66,*) CONV

! READ DERIVATIVE AT FIRST POINT - IF IT EXCEEDS 1.0D30 IT IS ASSUMED
! THAT THIS DERIVATIVE IS NOT NEEDED BY SPLINE FIT

      READ(66,*) YP1

! READ DERIVATIVE AT LAST POINT - IF IT EXCEEDS 1.0D30 IT IS ASSUMED
! THAT THIS DERIVATIVE IS NOT NEEDED BY SPLINE FIT

      READ(66,*) YPN

! READ TABLE [XVEC,YVEC]

      DO 20 I=1,NVEC
   20 READ(66,*) XVEC(I),YVEC(I)

! SUBTRACT VALUE AT INFINITY

      DO 21 I=1,NVEC
   21 YVEC(I)=YVEC(I)-VATINF

! CONVERT TO ATOMIC UNITS

      DO 22 I=1,NVEC
   22 YVEC(I)=YVEC(I)/CONV

      RETURN
      END
!
!=============================================================
!
      SUBROUTINE SINPOT(RBO,VBO,DBO,NBO,             &
     &                  RAD,VAD,DAD,NAD,REDMAS,			 &
     &                  RREL,VREL,DREL,NREL,				 &
     &                  RRAD,VRAD,DRAD,NRAD,ICASE,R,VSING)
!
! SINGLET
!
! ICASE=ISBOPP=1 FOR BORN OPPENHEIMER
! ICASE=ISADIA=2 FOR ADIABATIC
! ICASE=ISREL=3 FOR RELATIVISTIC
! ICASE=ISRAD=4 FOR RADIATIVE
!
! POTENTIAL IN ATOMIC UNITS
!
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION RBO(1000),VBO(1000),DBO(1000)
      DIMENSION RAD(1000),VAD(1000),DAD(1000)
      DIMENSION RREL(1000),VREL(1000),DREL(1000)
      DIMENSION RRAD(1000),VRAD(1000),DRAD(1000)

      ISBOPP=1
      ISADIA=2
      ISREL=3
      ISRAD=4

      IF(ICASE.EQ.ISBOPP) GO TO 1
      IF(ICASE.EQ.ISADIA) GO TO 2
      IF(ICASE.EQ.ISREL) GO TO 3
      IF(ICASE.EQ.ISRAD) GO TO 4

! SINGLET BORN OPPENHEIMER

    1 CALL VVSBO(RBO,VBO,DBO,NBO,R,VBORN)
      VSING=VBORN
      GO TO 5

! SINGLET ADIABATIC

    2 CALL VVSAD(RAD,VAD,DAD,NAD,REDMAS,R,VADIA)
      VSING=VADIA
      GO TO 5

! SINGLET RELATIVISTIC

    3 CALL VVSREL(RREL,VREL,DREL,NREL,R,VRELAT)
      VSING=VRELAT
      GO TO 5

! SINGLET RADIATIVE

    4 CALL VVSRAD(RRAD,VRAD,DRAD,NRAD,R,VRADIA)
      VSING=VRADIA
      GO TO 5

    5 CONTINUE

      RETURN
      END

!=============================================================

      SUBROUTINE TRIPOT(RBO,VBO,DBO,NBO,  &
     &                  RAD,VAD,DAD,NAD,	&
     &                  REDMAS,ICASE,R,VTRIP)

! TRIPLET

! ICASE=ITBOPP=7 FOR BORN OPPENHEIMER
! ICASE=ITADIA=8 FOR ADIABATIC

! POTENTIAL IN ATOMIC UNITS

      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION RBO(1000),VBO(1000),DBO(1000)
      DIMENSION RAD(1000),VAD(1000),DAD(1000)

      ITBOPP=7
      ITADIA=8

      IF(ICASE.EQ.ITBOPP) GO TO 7
      IF(ICASE.EQ.ITADIA) GO TO 8

! TRIPLET BORN OPPENHEIMER

    7 CALL VVTBO(RBO,VBO,DBO,NBO,R,VBORN)
      VTRIP=VBORN
      GO TO 1

! TRIPLET ADIABATIC

    8 CALL VVTAD(RAD,VAD,DAD,NAD,REDMAS,R,VADIA)
      VTRIP=VADIA
      GO TO 1

    1 CONTINUE

      RETURN
      END

!=============================================================

      SUBROUTINE VVSBO(RBO,VBO,DBO,NBO,R,VBORN)

! SINGLET BORN OPPENHEIMER POTENTIAL

      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION RBO(1000),VBO(1000),DBO(1000)

! R LESS THAN SHORT RANGE LIMIT - SET POTENTIAL TO A*EXP(-ALPHA*R/R)
! A AND ALPHA CHOSEN TO FIT FIRST TWO TABULATED POINTS

      IF(R.LT.RBO(1)) THEN
      IF(R.EQ.0.0D0) THEN
      VBORN=0.0D0
      ELSE
      ALPHA=-(DLOG(RBO(1)*VBO(1))-DLOG(RBO(2)*VBO(2)))/(RBO(1)-RBO(2))
      A=DEXP((RBO(2)*DLOG(RBO(1)*VBO(1))-RBO(1)*DLOG(RBO(2)*VBO(2)))/ &
     & (RBO(2)-RBO(1)))
      VBORN=A*DEXP(-ALPHA*R)/R
      ENDIF
      ENDIF

! R GREATER THAN LONG RANGE LIMIT - SET POTENTIAL TO LONG RANGE FORM

      IF(R.GT.RBO(NBO)) VBORN=SBOLNG(R)

! INTERPOLATE WITH SUBROUTINE FIT

      IF((R.GE.RBO(1)).AND.(R.LE.RBO(NBO))) THEN
     		CALL FIT(RBO,VBO,DBO,NBO,R,VBORN)
			END IF

      RETURN
      END

!=============================================================

      SUBROUTINE VVSAD(RAD,VAD,DAD,NAD,REDMAS,R,VADIA)

! SINGLET ADIABATIC POTENTIAL

      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION RAD(1000),VAD(1000),DAD(1000)

! R LESS THAN SHORT RANGE LIMIT - SET POTENTIAL TO ZERO

      IF(R.LT.RAD(1)) VADIA=0.0D0

! R GREATER THAN LONG RANGE LIMIT - SET POTENTIAL TO LONG RANGE FORM

      IF(R.GT.RAD(NAD)) VADIA=SADLNG(REDMAS,R)

! INTERPOLATE WITH SUBROUTINE FIT

      IF((R.GE.RAD(1)).AND.(R.LE.RAD(NAD))) THEN
     		CALL FIT(RAD,VAD,DAD,NAD,R,VADIA)
			END IF

      RETURN
      END

!=============================================================

      SUBROUTINE VVSREL(RREL,VREL,DREL,NREL,R,VRELAT)

! SINGLET RELATIVISTIC POTENTIAL

      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION RREL(1000),VREL(1000),DREL(1000)

! R LESS THAN SHORT RANGE LIMIT - SET POTENTIAL TO ZERO


      IF(R.LT.RREL(1)) VRELAT=0.0D0

! R GREATER THAN LONG RANGE LIMIT - SET POTENTIAL TO LONG RANGE FORM

      IF(R.GT.RREL(NREL)) VRELAT=SRELNG(R)

! INTERPOLATE WITH SUBROUTINE FIT

      IF((R.GE.RREL(1)).AND.(R.LE.RREL(NREL))) THEN
     		CALL FIT(RREL,VREL,DREL,NREL,R,VRELAT)
		  END IF	

      RETURN
      END

!=============================================================

      SUBROUTINE VVSRAD(RRAD,VRAD,DRAD,NRAD,R,VRADIA)

! SINGLET RADIATIVE POTENTIAL

      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION RRAD(1000),VRAD(1000),DRAD(1000)

! R LESS THAN SHORT RANGE LIMIT - SET POTENTIAL TO ZERO


      IF(R.LT.RRAD(1)) VRADIA=0.0D0

! R GREATER THAN LONG RANGE LIMIT - SET POTENTIAL TO LONG RANGE FORM


      IF(R.GT.RRAD(NRAD)) VRADIA=SRALNG(R)

! INTERPOLATE WITH SUBROUTINE FIT


      IF((R.GE.RRAD(1)).AND.(R.LE.RRAD(NRAD))) THEN
     		CALL FIT(RRAD,VRAD,DRAD,NRAD,R,VRADIA)
			END IF

      RETURN
      END

!=============================================================

      SUBROUTINE VVTBO(RBO,VBO,DBO,NBO,R,VBORN)

! TRIPLET BORN OPPENHEIMER POTENTIAL

      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION RBO(1000),VBO(1000),DBO(1000)

! R LESS THAN SHORT RANGE LIMIT - SET POTENTIAL TO A*EXP(-ALPHA*R/R)
! A AND ALPHA CHOSEN TO FIT FIRST TWO TABULATED POINTS

      IF(R.LT.RBO(1)) THEN
      IF(R.EQ.0.0D0) THEN
      VBORN=0.0D0
      ELSE
      ALPHA=-(DLOG(RBO(1)*VBO(1))-DLOG(RBO(2)*VBO(2)))/(RBO(1)-RBO(2))
      A=DEXP((RBO(2)*DLOG(RBO(1)*VBO(1))-RBO(1)*DLOG(RBO(2)*VBO(2)))/(RBO(2)-RBO(1)))
      VBORN=A*DEXP(-ALPHA*R)/R
      ENDIF
      ENDIF

! R GREATER THAN LONG RANGE LIMIT - SET POTENTIAL TO LONG RANGE FORM

      IF(R.GT.RBO(NBO)) VBORN=TBOLNG(R)

! INTERPOLATE WITH SUBROUTINE FIT

      IF((R.GE.RBO(1)).AND.(R.LE.RBO(NBO))) CALL FIT(RBO,VBO,DBO,NBO,R,VBORN)

      RETURN
      END
!
!=============================================================
!
      SUBROUTINE VVTAD(RAD,VAD,DAD,NAD,REDMAS,R,VADIA)

! TRIPLET ADIABATIC POTENTIAL

      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION RAD(1000),VAD(1000),DAD(1000)

! R LESS THAN SHORT RANGE LIMIT - SET POTENTIAL TO ZERO

      IF(R.LT.RAD(1)) VADIA=0.0D0

! R GREATER THAN LONG RANGE LIMIT - SET POTENTIAL TO LONG RANGE FORM

      IF(R.GT.RAD(NAD)) VADIA=TADLNG(REDMAS,R)

! INTERPOLATE WITH SUBROUTINE FIT

      IF((R.GE.RAD(1)).AND.(R.LE.RAD(NAD))) CALL FIT(RAD,VAD,DAD,NAD,R,VADIA)

      RETURN
      END
!
!=============================================================
!
      SUBROUTINE FIT(XVEC,YVEC,DVEC,NVEC,XIN,YOUT)

! SPLINE FIT TO VALUES IN TABLE [XVEC,YVEC]
! NUMBER OF TABULAR ENTRIES IS NVEC
! INPUT X VALUE IS XIN  OUTPUT Y VALUE IS YOUT

      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION XVEC(1000),YVEC(1000),DVEC(1000)

! WRITE WARNING AND STOP IF OUT OF RANGE

      IF((XIN.LT.XVEC(1)).OR.(XIN.GT.XVEC(NVEC))) GO TO 1
      GO TO 2
    1 WRITE(6,10)
   10 FORMAT(1X,'OUT OF RANGE')
      STOP

    2 CONTINUE
!
! SUBROUTINE SPLINE TO INITIALIZE SECOND DERIVATIVES FOR FIT
! MUST HAVE BEEN CALLED 
!
      CALL SPLINT(XVEC,YVEC,DVEC,NVEC,XIN,YOUT)

      RETURN
      END
!
!=============================================================
!
      DOUBLE PRECISION FUNCTION SADLNG(REDMAS,R)

! LONG RANGE SINGLET ADIABATIC

      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      ISING=1
      SADLNG=ADIABA(REDMAS,R,ISING)
      RETURN
      END
!
!=============================================================
!
      DOUBLE PRECISION FUNCTION TADLNG(REDMAS,R)

! LONG RANGE TRIPLET ADIABATIC

      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      ITRIP=-1
      TADLNG=ADIABA(REDMAS,R,ITRIP)
      RETURN
      END
!
!=============================================================
!
      DOUBLE PRECISION FUNCTION ADIABA(REDMAS,R,ICASE)

! LONG RANGE ADIABATIC
! ICASE=1  FOR SINGLET
! ICASE=-1 FOR TRIPLET

      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION A(12),RPOW(12),DER(12)

      A(6)=-6.499026705D0
      A(7)=0.0D0
      A(8)=-124.3990836D0
      A(9)=0.0D0
      A(10)=-3285.828415D0
      A(11)=3474.898D0
      A(12)=-122727.609D0
      AEXCH=-0.821D0

      RPOW(6)=R**(-6)
      DER(6)=-6.0D0*RPOW(6)/R
      DO 10 I=7,12
      RPOW(I)=RPOW(I-1)/R
   10 DER(I)=-I*RPOW(I)/R

! CALCULATE BORN OPPENHEIMER

      VDIR=0.0D0
      DO 11 I=6,12
   11 VDIR=VDIR+A(I)*RPOW(I)

! ADD EXCHANGE PART FOR SINGLET
! SUBTRACT EXCHANGE PART FOR TRIPLET

      EXCH=AEXCH*(R**2.5)*DEXP(-2.0D0*R)
      VDIR=VDIR+ICASE*EXCH

! CALCULATE DERIVATIVE

      DDIR=0.0D0
      DO 31 I=6,12
   31 DDIR=DDIR+A(I)*DER(I)

! ADD EXCHANGE PART FOR SINGLET
! SUBTRACT EXCHANGE PART FOR TRIPLET

      DEXCH=AEXCH*(2.5D0/R-2.0D0)*(R**2.5)*DEXP(-2.0D0*R)
      DDIR=DDIR+ICASE*DEXCH

! CALCULATE ADIABATIC PART

! ADIABATIC=-(E+R*DE/DR)/(TWICE REDUCED MASS)

      ADDIR=-(VDIR+R*DDIR)/(2.0D0*REDMAS)

      ADIABA=ADDIR

      RETURN
      END
!
!=============================================================
!
      DOUBLE PRECISION FUNCTION BOPP(R,ICASE)
!
! LONG RANGE BORN OPPENHEIMER 
!
! ICASE=1  FOR SINGLET
! ICASE=-1 FOR TRIPLET
!
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION A(12),RPOW(12)

      A(6)=-6.499026705D0
      A(7)=0.0D0
      A(8)=-124.3990836D0
      A(9)=0.0D0
      A(10)=-3285.828415D0
      A(11)=3474.898D0
      A(12)=-122727.609D0
      AEXCH=-0.821D0

      RPOW(6)=R**(-6)
      DO 10 I=7,12
   10 RPOW(I)=RPOW(I-1)/R

! CALCULATE BORN OPPENHEIMER

      VDIR=0.0D0
      DO 11 I=6,12
   11 VDIR=VDIR+A(I)*RPOW(I)

! ADD EXCHANGE PART FOR SINGLET
! SUBTRACT EXCHANGE PART FOR TRIPLET

      EXCH=AEXCH*(R**2.5)*DEXP(-2.0D0*R)
      VDIR=VDIR+ICASE*EXCH

      BOPP=VDIR

      RETURN
      END
!
!=============================================================
!
      DOUBLE PRECISION FUNCTION SBOLNG(R)

! SINGLET LONG RANGE BORN OPPENHEIMER

      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      ISING=1
      SBOLNG=BOPP(R,ISING)
      RETURN
      END
!
!=============================================================
!
      DOUBLE PRECISION FUNCTION TBOLNG(R)

! TRIPLET LONG RANGE BORN OPPENHEIMER

      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      ITRIP=-1
      TBOLNG=BOPP(R,ITRIP)
      RETURN
      END
!
!=============================================================
!
      DOUBLE PRECISION FUNCTION SRALNG(R)
!
! LONG RANGE SINGLET RADIATIVE
! TOTAL ASYMPTOTIC RADIATIVE CORRECTION IN HARTREE
! REFERRED TO ASYMPTOTIC VALUE AT INFINITY 
! SET TO ZERO TEMPORARILY
!
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)

      SRALNG=0.0D0
      RETURN
      END
!
!=============================================================
!
      DOUBLE PRECISION FUNCTION SRELNG(R)
!
! LONG RANGE SINGLET RELATIVISTIC
! TOTAL ASYMPTOTIC RELATIVISTIC CORRECTION IN HARTREE
! REFERRED TO ASYMPTOTIC VALUE AT INFINITY 
! SET TO ZERO TEMPORARILY
!
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)

      SRELNG=0.0D0
      RETURN
      END
